plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    //QueryDsl
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
    id 'java'
    id 'jacoco'
}

group = 'com.sollertia'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

bootJar {
    copy {
        from "build/reports/jacoco/test/html"
        into "src/main/resources/static/docs/jacoco"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    //swagger
    implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
    implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'

    //jwt
    implementation 'io.jsonwebtoken:jjwt:0.9.1'

    //redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis:2.5.3'

    //fix enum nullable (enum.When)
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    //health check
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //validation
    implementation "org.springframework.boot:spring-boot-starter-validation"

    //powermock
    testImplementation 'org.powermock:powermock-module-junit4:2.0.4'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.4'

    //jasypt
    implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4"

    //QueryDsl
    implementation 'com.querydsl:querydsl-jpa'
    implementation 'com.querydsl:querydsl-apt'
}



test {
    useJUnitPlatform()
}

//QueryDSL
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main.java.srcDir querydslDir
}
configurations {
    querydsl.extendsFrom compileClasspath

}
compileQuerydsl.doFirst {
    if ( file(querydslDir).exists() )
        delete(file(querydslDir))
}
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

